plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'de.inetsoftware'
archivesBaseName = 'jwebassembly-api'
version = '0.4'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly     'com.google.code.findbugs:jsr305:3.0.2'

    // dependencies for the emulator
    compileOnly     'org.ow2.asm:asm:+'
    def fxVersion = getJavaFxVersion()
    if( fxVersion > 8 ) { // version 8 is build into the OpenJDK
        // JavaFX depends the platform and JDK version
        def platform = getJavaFxPlatform()
        def modules = ['javafx-base', 'javafx-controls', 'javafx-graphics', 'javafx-media', 'javafx-web' ]
        for (String module : modules) {
            compileOnly 'org.openjfx:' + module + ':' + fxVersion + '.+:' + platform
            testCompile 'org.openjfx:' + module + ':' + fxVersion + '.+:' + platform
        }
    }

    testCompile     'junit:junit:+'
    testCompile     'org.ow2.asm:asm:+'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test'
        }
    }
}

/**
* The String for Javafx classifier
*/
def getJavaFxPlatform() {
    def osname = System.properties['os.name'].toLowerCase()
    if( osname.contains( 'windows' ) ) {
        return 'win'
    } else if( osname.contains( 'mac' ) ) {
        return 'mac'
    }
    return 'linux'
}

/**
* The Javafx version that is compatible with the current JDK
*/
def getJavaFxVersion() {
    def fxVersion = org.gradle.util.VersionNumber.parse( System.properties['java.version'] ).getMajor()
    if( fxVersion > 8 && fxVersion < 11 ) {
        // There is no version 9 and 10, version 8 is build into the OpenJDK
        fxVersion = 11
    }
    return fxVersion;
}

jar {
    manifest {
        attributes( 'Specification-Title': 'JWebAssembly-API',
                    'Specification-Vendor': 'i-net software',
                    'Specification-Version': version,
                    'Implementation-Title': 'JWebAssembly-API',
                    'Implementation-Vendor': 'i-net software',
                    'Implementation-Version': version,
                    'Premain-Class': 'de.inetsoftware.jwebassembly.emulator.PremainEmulator',
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

build {
    dependsOn sourcesJar
    dependsOn javadocJar
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    dependsOn 'jar'
    jvmArgs "-javaagent:${jar.archivePath}"
    testLogging {
        showStandardStreams = true
        showStackTraces = true
        exceptionFormat = 'full'
        events 'passed', 'skipped', 'failed'
    }
}

/****************************************
 * Deploy to Sonatype
 ****************************************/
publishing {
    publications {
        JWebAssemblyApi(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifactId archivesBaseName
            pom {
                name = 'JWebAssembly-API'
                description = 'The API for using WebAssembly features in your Java classes.'
                url = 'https://github.com/i-net-software/JWebAssembly-API'
                developers {
                    developer {
                        id = 'Horcrux7'
                        name = 'Volker Berlin'
                        email = 'vberlin@inetsoftware.de'
                        organization = 'i-net software'
                        organizationUrl = 'https://www.inetsoftware.de'
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:i-net-software/JWebAssembly-API.git'
                    developerConnection = 'scm:git:git@github.com:i-net-software/JWebAssembly-API.git'
                    url = 'https://github.com/i-net-software/JWebAssembly-API'
                }
            }
        }
    }
    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            if (project.hasProperty("ossrhUsername") ) {
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}

signing {
    if (project.hasProperty("signing.keyId") ){
       sign publishing.publications.JWebAssemblyApi

        // does not create sha256 and sha512 checksums
        System.setProperty('org.gradle.internal.publish.checksums.insecure', 'true' )
    }
}
